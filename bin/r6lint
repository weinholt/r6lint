#!/usr/bin/env scheme-script
;; -*- mode: scheme; coding: utf-8 -*- !#
;; Copyright © 2017 Göran Weinholt <goran@weinholt.se>
;; SPDX-License-Identifier: MIT

;; Permission is hereby granted, free of charge, to any person obtaining a
;; copy of this software and associated documentation files (the "Software"),
;; to deal in the Software without restriction, including without limitation
;; the rights to use, copy, modify, merge, publish, distribute, sublicense,
;; and/or sell copies of the Software, and to permit persons to whom the
;; Software is furnished to do so, subject to the following conditions:

;; The above copyright notice and this permission notice shall be included in
;; all copies or substantial portions of the Software.

;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
;; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.
#!r6rs

(import (r6lint lib linter)
        (r6lint psyntax compat)
        (r6lint psyntax library-manager)
        (rnrs (6)))

;; The output format is nicked from pylint running under flycheck:

;; {path}:{line}:{column}:{C}:{msg_id}:{msg}

(define (message-printer path line column level id message)
  (display path)
  (display #\:)
  (display line)
  (display #\:)
  (display column)
  (display #\:)
  (display (case level
             ((refactor) 'R)
             ((convention) 'C)
             ((warning) 'W)
             ((error) 'E)
             ((fatal) 'F)
             (else level)))
  (display #\:)
  (display id)
  (display #\:)
  (display message)
  (newline))

(define (parse-args args)
  (define (help . msg)
    (let ((p (current-error-port)))
      (when msg (display (car msg) p) (newline p) (newline p))
      (display "r6lint - R6RS Scheme linter

Usage:
  r6lint [-c|--config <filename>] [--] <filename>
" p)
      (exit 1)))
  (let lp ((filename #f)
           (config-file #f)
           (args args))
    (cond ((null? args)
           (unless filename
             (help "ERROR: No filename given."))
           (values filename config-file))
          ((member (car args) '("--config" "-c"))
           (if (null? (cdr args)) (help "ERROR: --config needs an argument"))
           (lp filename (cadr args) (cddr args)))
          ((string=? (car args) "--")
           (if (not (= (length args) 2)) (help "ERROR: following -- must be only a filename"))
           (if filename (help "ERROR: you can't have it both ways, use -- or don't"))
           (lp (cadr args) config-file (cddr args)))
          (else
           (if filename (help "ERROR: extra arguments on command line"))
           (lp (car args) config-file (cdr args))))))

(define (main filename config-file)
  (let ((config (if (and config-file (file-exists? config-file))
                    (with-input-from-file config-file read)
                    '())))
    ;; XXX: Should allow the path of the script/library to be used
    (parameterize ((library-path (cond ((assq 'library-path config) => cdr)
                                       (else (library-path)))))
      (call-with-port (open-input-file filename)
        (lambda (p)
          (lint filename p message-printer))))))

(call-with-values (lambda () (parse-args (cdr (command-line)))) main)

(flush-output-port (current-output-port))
